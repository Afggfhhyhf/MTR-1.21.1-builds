import org.mtr.mod.BuildTools

plugins {
	id "java"
	id "com.github.johnrengelman.shadow" version "+" apply false
}

group "org.mtr.mod"
version rootProject.properties.version
final List<String> minecraftLibraries = BuildTools.getLibraries(minecraftVersion)

subprojects {
	apply plugin: "java"
	apply plugin: "com.github.johnrengelman.shadow"

	configurations {
		shadowImplementation
		implementation.extendsFrom shadowImplementation
	}

	dependencies {
		implementation "com.google.code.findbugs:jsr305:+"
		shadowImplementation("com.corundumstudio.socketio:netty-socketio:+") { exclude group: "io.netty", module: "netty-transport-native-epoll" }
		shadowImplementation "org.msgpack:msgpack-core:+"
		shadowImplementation "io.socket:socket.io-client:+"
		shadowImplementation("com.github.jonafanho:Socket-IO-Webserver:master-SNAPSHOT") { exclude group: "io.netty", module: "netty-transport-native-epoll" }
		shadowImplementation "org.mtr:Minecraft-Mappings-common:0.0.1"
		shadowImplementation "org.mtr:Transport-Simulation-Core:0.0.1"
		compileOnly "xyz.jpenilla:squaremap-api:+"
		compileOnly "net.kyori:adventure-api:+"
		compileOnly "com.test:Dynmap:3.2.1"
	}

	repositories {
		flatDir { dirs "../libs" }
		maven { url "https://jitpack.io" }
	}

	shadowJar {
		configurations = [project.configurations.shadowImplementation]
		dependencies { exclude(dependency { minecraftLibraries.contains("${it.moduleGroup}:${it.moduleName}:".toString()) }) }
	}

	tasks.withType(Javadoc).tap {
		configureEach { javadoc.options.addStringOption("Xdoclint:none", "-quiet") }
	}
}
