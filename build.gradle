plugins {
    id "fabric-loom" version "0.10-SNAPSHOT"
    id "maven-publish"
}

def is116 = (project.minecraft_version as String).startsWith("1.16")
def is117 = (project.minecraft_version as String).startsWith("1.17")

if (is116) {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
} else {
    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
}

archivesBaseName = project.archives_base_name
version = (project.minecraft_version as String) + "-" + (project.mod_version as String)
group = project.maven_group as String

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${is116 ? project.yarn_mappings_1_16 : project.yarn_mappings_1_17}:v2"
    modImplementation "net.fabricmc:fabric-loader:${is116 ? project.loader_version_1_16 : project.loader_version_1_17}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${is116 ? project.fabric_version_1_16 : project.fabric_version_1_17}"
    modImplementation "com.terraformersmc:modmenu:${is116 ? project.mod_menu_version_1_16 : project.mod_menu_version_1_17}"
    modImplementation "com.github.BlueMap-Minecraft:BlueMapAPI:${is116 ? project.blue_map_version_1_16 : project.blue_map_version_1_17}"
    modImplementation "de.guntram.mcmod:crowdin-translate:${is116 ? project.crowdin_translate_version_1_16 : project.crowdin_translate_version_1_17}"
    modImplementation "org.eclipse.jetty:jetty-http:${project.jetty_version}"
    modImplementation "org.eclipse.jetty:jetty-io:${project.jetty_version}"
    modImplementation "org.eclipse.jetty:jetty-security:${project.jetty_version}"
    modImplementation "org.eclipse.jetty:jetty-server:${project.jetty_version}"
    modImplementation "org.eclipse.jetty:jetty-servlet:${project.jetty_version}"
    modImplementation "org.eclipse.jetty:jetty-util:${project.jetty_version}"
    modImplementation "org.eclipse.jetty:jetty-util-ajax:${project.jetty_version}"
    modImplementation "javax.servlet:javax.servlet-api:${project.servlet_api_version}"
    include "de.guntram.mcmod:crowdin-translate:${is116 ? project.crowdin_translate_version_1_16 : project.crowdin_translate_version_1_17}"
    include "org.eclipse.jetty:jetty-http:${project.jetty_version}"
    include "org.eclipse.jetty:jetty-io:${project.jetty_version}"
    include "org.eclipse.jetty:jetty-security:${project.jetty_version}"
    include "org.eclipse.jetty:jetty-server:${project.jetty_version}"
    include "org.eclipse.jetty:jetty-servlet:${project.jetty_version}"
    include "org.eclipse.jetty:jetty-util:${project.jetty_version}"
    include "org.eclipse.jetty:jetty-util-ajax:${project.jetty_version}"
    include "javax.servlet:javax.servlet-api:${project.servlet_api_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"

    if (is116) {
        def targetVersion = 8
        if (JavaVersion.current().isJava9Compatible()) {
            it.options.release = targetVersion
        }
    } else {
        it.options.release = 16
    }
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${archivesBaseName}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {}
}

allprojects {
    repositories {
        maven {
            name = "BlueMap"
            url = "https://jitpack.io/"
        }
        maven {
            url = "https://minecraft.guntram.de/maven/"
        }
        maven {
            url = "https://maven.terraformersmc.com/"
        }
    }
}
